plugins {
    id "java"
    id "base.base-conventions"
    id "base.application-conventions"
    id "base.fill-build-constants"
    id "viaproxy.publishing-conventions"
    id "net.raphimc.class-token-replacer" version "1.1.7"
    id "xyz.wagyourtail.jvmdowngrader" version "1.3.3"
}

configurations {
    includeInJarJ8
}

repositories {
    maven {
        name = "ViaVersion"
        url = "https://repo.viaversion.com"
    }
    maven {
        name = "Lenni0451"
        url = "https://maven.lenni0451.net/everything"
    }
    maven {
        name = "OpenCollab Releases"
        url = "https://repo.opencollab.dev/maven-releases"
    }
    maven {
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/maven-snapshots"
    }
    maven {
        name = "Minecraft Libraries"
        url = "https://libraries.minecraft.net"

        content {
            includeGroup "com.mojang"
        }
    }
    maven {
        name = "Jitpack"
        url = "https://jitpack.io"

        content {
            includeGroup "com.github.Oryxel"
        }
    }
}

dependencies {
    includeInJar "com.viaversion:viaversion-common:5.4.2-SNAPSHOT"
    includeInJar "com.viaversion:viabackwards-common:5.4.1"
    includeInJar "com.viaversion:viarewind-common:4.0.8"
    includeInJar "net.raphimc:ViaLegacy:3.0.10"
    includeInJar "com.viaversion:viaaprilfools-common:4.0.3"
    includeInJar("net.raphimc:ViaBedrock:0.0.20-SNAPSHOT") {
        exclude group: "io.netty"
        exclude group: "io.jsonwebtoken"
    }
    includeInJar("com.viaversion:vialoader:4.0.4") {
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    includeInJar "com.google.code.gson:gson:2.13.1"
    includeInJar "com.formdev:flatlaf:3.6"
    includeInJar "com.formdev:flatlaf-extras:3.6"
    includeInJar "org.apache.commons:commons-lang3:3.18.0"
    includeInJar "commons-io:commons-io:2.19.0"
    includeInJar "net.sf.jopt-simple:jopt-simple:5.0.4"
    includeInJar "org.apache.logging.log4j:log4j-core:2.25.0"
    includeInJar "org.apache.logging.log4j:log4j-slf4j-impl:2.25.0"
    includeInJar "org.jline:jansi:3.30.4"
    includeInJar("com.mojang:authlib:6.0.58") {
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    includeInJar "net.lenni0451.classtransform:mixinstranslator:1.14.1"
    includeInJar "net.lenni0451.classtransform:mixinsdummy:1.14.1"
    includeInJar "net.lenni0451.classtransform:additionalclassprovider:1.14.1"
    includeInJar "net.lenni0451:Reflect:1.5.0"
    includeInJar "net.lenni0451:LambdaEvents:2.4.2"
    includeInJar("net.lenni0451:MCPing:1.4.4") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    includeInJar "net.lenni0451.commons:swing:1.7.1"
    includeInJar "net.lenni0451.commons:brigadier:1.7.1"
    includeInJar("net.raphimc.netminecraft:all:3.1.2") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    includeInJar("net.raphimc:MinecraftAuth:4.1.1") {
        exclude group: "com.google.code.gson", module: "gson"
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    includeInJar("org.cloudburstmc.netty:netty-transport-raknet:1.0.0.CR3-SNAPSHOT") {
        exclude group: "io.netty"
    }
    includeInJar "gs.mclo:api:5.0.2"
    includeInJar "net.lenni0451:optconfig:1.1.1"

    includeInJarJ8(compileOnly("xyz.wagyourtail.jvmdowngrader:jvmdowngrader:1.3.3"))
    includeInJarJ8 "xyz.wagyourtail.jvmdowngrader:jvmdowngrader-java-api:1.3.3:downgraded-8"
}

application {
    mainClass = "net.raphimc.viaproxy.ViaProxy"
}

jar {
    manifest {
        attributes(
                "Launcher-Agent-Class": application.mainClass
        )
    }
}

downgradeJar {
    dependsOn(configurations.includeInJarJ8)
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.includeInJarJ8.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }

    downgradeTo = JavaVersion.VERSION_1_8
    ignoreWarningsIn.add("net/lenni0451/commons/httpclient/executor/HttpClientExecutor")
    archiveClassifier = null
    archiveVersion = project.version + "+java8"
}
build.finalizedBy("downgradeJar")

tasks.javadoc.enabled = false
